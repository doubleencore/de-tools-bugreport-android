apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

group='com.github.doubleencore'

repositories {
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)

        versionCode Integer.parseInt(project.PROJECT_VERSION_CODE)
        versionName project.PROJECT_VERSION_NUMBER
    }
}

version project.PROJECT_VERSION_NUMBER

dependencies {
    compile 'com.github.doubleencore:de-tools-build-util-android:0.1.1'
    compile 'com.getbase:floatingactionbutton:1.10.1'
    compile "com.android.support:support-v4:$rootProject.appCompatVersion"
    compile "commons-io:commons-io:2.4"
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'net.gotev:uploadservice:2.1@aar'
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
